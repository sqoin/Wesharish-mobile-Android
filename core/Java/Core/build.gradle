apply plugin: 'com.android.library'

android {
    compileSdkVersion 28
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // Link Gradle to the CMake script
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"  //path can only be set outside (in android block)
        }
    }


    // https://developer.android.com/studio/build/build-variants
    //    "defaultConfig actually belongs to the ProductFlavor class."
    //
    // http://google.github.io/android-gradle-dsl/current/com.android.build.gradle.BaseExtension.html#com.android.build.gradle.BaseExtension:defaultConfig(org.gradle.api.Action)
    //    "Specifies defaults for variant properties that the Android plugin applies to all build variants.
    //    "You can override any defaultConfig property when configuring product flavors."
    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // Specify ABI configuration for the Core native Libraries
        ndk {
            abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
        }
    }

    flavorDimensions "network"
    productFlavors {

        onMainnet {
            dimension "network"
        }

        onTestnet {
            dimension "network"
            externalNativeBuild {
                cmake {
                    cFlags "-DBITCOIN_TESTNET"
                }
            }
        }
    }

    // http://google.github.io/android-gradle-dsl/current/com.android.build.gradle.BaseExtension.html#com.android.build.gradle.BaseExtension:buildTypes(org.gradle.api.Action)
    // "Encapsulates all build type configurations for this project"
    buildTypes {

        // Default: required
        release {
            minifyEnabled false

            // Ensure that the -DNDEBUG compiler flag IS NOT set thereby ensuring that `assert(c)``
            // evaluates `c` (for side-effects - but we've removed side-effects anyhow)
            debuggable true
            jniDebuggable true
        }

        // Default: created on demand with 'debuggable true'
        debug {
            minifyEnabled false

            debuggable true
            jniDebuggable true
            externalNativeBuild {
                cmake {
                    cFlags "-DDEBUG", "-DBITCOIN_DEBUG"
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
